apply {
    plugin 'com.android.application'
    if (Config.depConfig.plugin_api.isApply) {
        plugin Config.depConfig.plugin_api.pluginId
    }
    if (Config.depConfig.plugin_bus.isApply) {
        plugin Config.depConfig.plugin_bus.pluginId
    }
}

if (Config.depConfig.plugin_bus.isApply) {
    bus {
        onlyScanLibRegex = '^([:]|(com\\.blankj)).+$'
    }
}

if (Config.depConfig.plugin_api.isApply) {
    api {
        onlyScanLibRegex = '^([:]|(com\\.blankj)).+$'
    }
}

android {
    compileSdkVersion Config.compileSdkVersion
    buildToolsVersion Config.buildToolsVersion

    defaultConfig {
        applicationId Config.applicationId + suffix
        minSdkVersion Config.minSdkVersion
        targetSdkVersion Config.targetSdkVersion
        versionCode Config.versionCode
        versionName Config.versionName

        multiDexEnabled true
        resValue "string", "app_name", Config.appName + suffix

    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/*'
    }

    dexOptions {
        preDexLibraries true
        javaMaxHeapSize "8g"
        maxProcessCount 8
        dexInProcess = true
    }

    compileOptions {
        sourceCompatibility Config.sourceCompatibility
        targetCompatibility Config.targetCompatibility
    }
}

dependencies {
    // LeakCanary
    debugImplementation Config.depConfig.leakcanary.dep


    // 根据 Config.pkgConfig 来依赖所有 pkg
    for (def entrySet : ConfigUtils.getApplyPkgs().entrySet()) {
        api entrySet.value.dep
    }

    if (Config.depConfig.feature_mock.isApply) {
        api Config.depConfig.feature_mock.dep
    }
}

def getSuffix() {
    if (project.name == "feature_launcher_app") return ""
    return "." + project.name.substring("feature_".length(), project.name.length() - "_app".length())
}